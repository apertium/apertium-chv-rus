Multichar_Symbols

! Multichar symbols are enclosed in < > according to Apertium style, 
! archigraphemes are enclosed in { } according to Omorfi style

%<n%>        ! Noun
%<np%>       ! Proper noun
%<v%>        ! Verb
%<adv%>      ! Adverb
%<num%>      ! Numeral
%<adj%>      ! Adjective
%<prn%>      ! Pronoun
%<post%>     ! Postposition
%<cnjcoo%>   ! Coordinating conjunction
%<cnjsub%>   ! Subordinating conjunction
%<part%>     ! Particle

%<der‣caus%> ! Derivation (V→V): Causative

%<sg%>       ! Singular
%<pl%>       ! Plural
%<attr%>     ! Attributive
%<pred%>     ! Predicative
%<abs%>      ! Absolutive
%<obj%>      ! Objective 
%<gen%>      ! Genitive 
%<ins%>      ! Instrumental 
%<loc%>      ! Locative
%<abl%>      ! Ablative
%<dur%>      ! Durative
%<pres%>     ! Present
%<p1%>       ! First person
%<p2%>       ! Second person
%<p3%>       ! Third person
%<def%>      ! Definite
%<ind%>      ! Indefinite
%<pers%>     ! Personal
%<top%>      ! Toponym
%<ant%>      ! Anthroponym 
%<abbr%>     ! Abbreviation

%<pp%>       ! Past participle
%<pprs%>     ! Present participle
%<fp%>       ! Future participle
%<inf%>      ! Verbal noun / infinitive 
%<conv%>     ! Converb 
%<neg%>      ! Negative

%<clb%>      ! Clause level boundary
%<lquot%>    ! Left quote 
%<rquot%>    ! Right quote


%>           ! Morpheme boundary
%{а%}        ! Archivowel: а or е 
%{ӑ%}        ! Archivowel: ӑ or ӗ
%{р%}        ! Archiconsonant: р оr т 
%{н%}        ! Archiconsonant: н or м 
%{ь%}        ! ь or ''

LEXICON Root

NominalStems ;
Pronouns ;
VerbStems ;
ProperNouns ;
AdjStems ;
Numerals ;
Adverbs ; 
Postpos ; 
Particles ; 
Conjunctions ; 
Punct ; 

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! Paradigms 
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

LEXICON Decl1 

%<abs%>: # ;

! 1.  -ø          Optional with any stem
! 2.  >Ăn         After polysyllabic consonant stems and vocalic stems in -U employing
!                their stem alternant in -Ăv to replace U; some monosyllabic consonant
!                stems
! 3.  >n or >Ăn   After polysyllabic consonant stems except U and A
! 4.  C¹C¹Ă>N     After polysyllabic vowel stems in >-C¹Ă
! 5.  C¹C²Ă>n     After polysyllabic vowel stems in >-C¹C²Ă
! 6.  C¹>Ǎn       After borrowed polysyllabic stems in C¹C¹
! 7.  C¹C¹>Ăn     After some monosyllabic stems in C¹
! 8.  >n or >jěn  After native and foreign stems in -i
! 9.  -ă>n        After borrowed vowel stems in -a, -ja, -e, -u and some in -o
! 10. -o>n        After some borrowed vowel stems in -o
! 11. -s>ăn       After borrowed stems in -st'      

%<gen%>:%>%{а%}н # ;        ! 

%<obj%>%<ind%>: # ;          ! 
%<obj%>%<def%>:%>%{а%} # ;          ! 
%<loc%>:%>%{р%}%{а%} # ;        ! 
%<abl%>:%>%{р%}%{а%}н # ;        ! 
%<ins%>:%>п%{а%} # ;        ! 
%<fin%>:%>ш%{ӑ%}н # ;      ! 
%<priv%>:%>с%{ӑ%}р # ;      ! 
%<term%>:%>чен # ;          ! 
%<dist%>:%>серен # ;          ! 

LEXICON Nreg

%<n%>: Decl1 ;
%<n%>%<pl%>:%>се%{н%} Decl1 ;

LEXICON Nprop

%<np%>: Decl1 ;

LEXICON NP-TOP

%<np%>%<top%>: Decl1 ;

LEXICON NP-ANT-MSC 

%<np%>%<ant%>%<m%>: Decl1 ;

LEXICON NUMshort

%<num%>%<attr%>: # ;

LEXICON NUMlong

%<num%>%<pred%>: # ;

LEXICON NUM-ABBR

%<num%>%<abbr%>: # ;


LEXICON ADV 

%<adv%>: # ;

LEXICON POST 

%<post%>: # ;

LEXICON CLB

%<clb%>: # ;

LEXICON ADJ

%<adj%>: # ;

LEXICON CC 

%<cnjcoo%>: # ; 

LEXICON CS

%<cnjsub%>: # ; 

LEXICON PART

%<part%>: # ; 

LEXICON V-PRES-CONJ

%<dur%>%<pres%>%<p1%>%<sg%>:%>т%{ӑ%}р # ;
%<dur%>%<pres%>%<p2%>%<sg%>:%>т%{ӑ%}н # ; 
%<dur%>%<pres%>%<p3%>%<sg%>:%>т%{ь%} # ;
%<dur%>%<pres%>%<p1%>%<pl%>:%>тп%{ӑ%}р # ;
%<dur%>%<pres%>%<p2%>%<pl%>:%>т%{ӑ%}р # ; 
%<dur%>%<pres%>%<p3%>%<pl%>:%>шшӗ # ;

LEXICON C-PRES-CONJ

%<dur%>%<pres%>%<p1%>%<sg%>:%>%{а%}т%{ӑ%}р # ;
%<dur%>%<pres%>%<p2%>%<sg%>:%>%{а%}т%{ӑ%}н # ; 
%<dur%>%<pres%>%<p3%>%<sg%>:%>%{а%}т%{ь%} # ;
%<dur%>%<pres%>%<p1%>%<pl%>:%>%{а%}тп%{ӑ%}р # ;
%<dur%>%<pres%>%<p2%>%<pl%>:%>%{а%}т%{ӑ%}р # ; 
%<dur%>%<pres%>%<p3%>%<pl%>:%>^{а%}шшӗ # ;

LEXICON V-STEM-CONJ

%<pprs%>:%>к%{а%}н Decl1 ;
%<pp%>:%>н%{ӑ%} # ;
%<fp%>:%>%{а%}с # ;
%<inf%>:%>м%{а%} # ;
%<conv%>:%>с%{а%} # ;
%<neg%>:%>м%{а%}с V-PRES-CONJ ;

!%<der‣caus%>%<v%>:%>тар Vreg ; 

LEXICON C-STEM-CONJ

%<pprs%>:%>к%{а%}н Decl1 ;
%<pp%>:%>н%{ӑ%} # ;
%<fp%>:%>%{а%}с # ;
%<inf%>:%>м%{а%} # ;
%<conv%>:%>с%{а%} # ;
%<neg%>:%>м%{а%}с C-PRES-CONJ ;
C-PRES-CONJ ; 

!%<der‣caus%>%<v%>:%>тар Vreg ; 


LEXICON V-STEM-V-REG

%<v%>: V-STEM-CONJ ;

LEXICON C-STEM-V-REG

%<v%>: C-STEM-CONJ ;

LEXICON Pronouns 

!! Review forms with -(ăn)

епĕ%<prn%>%<pers%>%<p1%>%<sg%>%<abs%>:епĕ # ;
епĕ%<prn%>%<pers%>%<p1%>%<sg%>%<obj%>:ман # ;
епĕ%<prn%>%<pers%>%<p1%>%<sg%>%<obj%>:манӑн # ;
епĕ%<prn%>%<pers%>%<p1%>%<sg%>%<gen%>:мана # ;
епĕ%<prn%>%<pers%>%<p1%>%<sg%>%<loc%>:манра # ;
епĕ%<prn%>%<pers%>%<p1%>%<sg%>%<abl%>:манран # ;
епĕ%<prn%>%<pers%>%<p1%>%<sg%>%<ins%>:манпа # ;

есĕ%<prn%>%<pers%>%<p2%>%<sg%>%<abs%>:есĕ # ;
есĕ%<prn%>%<pers%>%<p2%>%<sg%>%<obj%>:сан # ;
есĕ%<prn%>%<pers%>%<p2%>%<sg%>%<obj%>:санӑн # ;
есĕ%<prn%>%<pers%>%<p2%>%<sg%>%<gen%>:сана # ;
есĕ%<prn%>%<pers%>%<p2%>%<sg%>%<loc%>:санра # ;
есĕ%<prn%>%<pers%>%<p2%>%<sg%>%<abl%>:санран # ;
есĕ%<prn%>%<pers%>%<p2%>%<sg%>%<ins%>:санпа # ;

вӑл%<prn%>%<pers%>%<p3%>%<sg%>%<abs%>:вӑл # ;
вӑл%<prn%>%<pers%>%<p3%>%<sg%>%<obj%>:ун # ;
вӑл%<prn%>%<pers%>%<p3%>%<sg%>%<obj%>:унӑн # ;
вӑл%<prn%>%<pers%>%<p3%>%<sg%>%<gen%>:ӑна # ;
вӑл%<prn%>%<pers%>%<p3%>%<sg%>%<loc%>:унра # ;
вӑл%<prn%>%<pers%>%<p3%>%<sg%>%<abl%>:унран # ;
вӑл%<prn%>%<pers%>%<p3%>%<sg%>%<ins%>:унпа # ;

епир%<prn%>%<pers%>%<p1%>%<pl%>%<abs%>:епир #;
епир%<prn%>%<pers%>%<p1%>%<pl%>%<obj%>:пире #;
епир%<prn%>%<pers%>%<p1%>%<pl%>%<gen%>:пирĕн #;
епир%<prn%>%<pers%>%<p1%>%<pl%>%<loc%>:пирĕнте #;
епир%<prn%>%<pers%>%<p1%>%<pl%>%<abl%>:пирĕнтен #;
епир%<prn%>%<pers%>%<p1%>%<pl%>%<ins%>:пирĕнпе #;

есир%<prn%>%<pers%>%<p2%>%<pl%>%<abs%>:есир #;
есир%<prn%>%<pers%>%<p2%>%<pl%>%<obj%>:сире #;
есир%<prn%>%<pers%>%<p2%>%<pl%>%<gen%>:сирĕн #;
есир%<prn%>%<pers%>%<p2%>%<pl%>%<loc%>:сирĕнте #;
есир%<prn%>%<pers%>%<p2%>%<pl%>%<abl%>:сирĕнтен #;
есир%<prn%>%<pers%>%<p2%>%<pl%>%<ins%>:сирĕнпе #;

вĕсем%<prn%>%<pers%>%<p3%>%<pl%>%<abs%>:вĕсем #;
вĕсем%<prn%>%<pers%>%<p3%>%<pl%>%<obj%>:вĕсен #;
вĕсем%<prn%>%<pers%>%<p3%>%<pl%>%<obj%>:вĕсенĕн #;
вĕсем%<prn%>%<pers%>%<p3%>%<pl%>%<gen%>:вĕсене #;
вĕсем%<prn%>%<pers%>%<p3%>%<pl%>%<loc%>:вĕсенче #;
вĕсем%<prn%>%<pers%>%<p3%>%<pl%>%<abl%>:вĕсенчен #;
вĕсем%<prn%>%<pers%>%<p3%>%<pl%>%<ins%>:вĕсемпе #;

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! Entries 
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

LEXICON Numerals

пĕр NUMshort ;
пĕр:пĕрре NUMlong ;

пилĕк NUMshort ;
пилĕк:пиллĕк NUMlong ;

млн%.:млн%. NUM-ABBR ;

LEXICON AdjStems

шĕнĕ ADJ ;

LEXICON NominalStems

завод Nreg ; 
фабрик Nreg ; 
католик Nreg ; 
кун Nreg ;
чечек Nreg ; 

тӳпе Nreg ;
çĕре Nreg ; 
ту Nreg ;
уй Nreg ;
ача Nreg ;
кĕнеке Nreg ;

LEXICON ProperNouns

Турӑ Nprop ; 
Шупашкар N-TOP ; 
Мадрид N-TOP ; 
Петĕр NP-ANT-MSC ; 

LEXICON Adverbs

пу% çламӑшенче ADV ;
нумай ADV ;
киле ADV ;

LEXICON Postpos 

урлӑ POST ; 
пĕрле POST ; 
витĕр POST ; 
пек POST ; 
таран POST ; 
тӑрӑх POST ; 
пула POST ; 
пирки POST ; 
валли POST ; 
кура POST ; 
хирĕç POST ; 
пуçне POST ; 

LEXICON Conjunctions 

тата CC ; 

LEXICON Particles 

акӑ PART ;

LEXICON VerbStems 

пул C-STEM-V-REG ;
кил C-STEM-V-REG ;
ил C-STEM-V-REG ;
сыp C-STEM-V-REG ;
тух C-STEM-V-REG ;
тӑва V-STEM-V-REG ;
кая V-STEM-V-REG ;
сывла V-STEM-V-REG ;
вула V-STEM-V-REG ;
ĕçле V-STEM-V-REG ;

пырса% кала V-STEM-V-REG ;
тухса% кай C-STEM-V-REG ;

LEXICON Punct

. CLB ; 
? CLB ; 
%! CLB ; 
»%<rquot%>:» # ;
«%<lquot%>:« # ; 
