!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!   M O R P H O L O G I C A L · T R A N S D U C E R · F O R · C H U V A S H !!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

Multichar_Symbols

! Multichar symbols are enclosed in < > according to Apertium style, 
! archigraphemes are enclosed in { } according to Omorfi style

%<n%>        ! Noun
%<np%>       ! Proper noun
%<v%>        ! Verb
%<adv%>      ! Adverb
%<num%>      ! Numeral
%<adj%>      ! Adjective
%<ij%>       ! Interjection
%<prn%>      ! Pronoun
%<post%>     ! Postposition
%<cnjcoo%>   ! Coordinating conjunction
%<cnjsub%>   ! Subordinating conjunction
%<part%>     ! Particle

%<der‣caus%> ! Derivation (V→V): Causative

%<sg%>       ! Singular
%<pl%>       ! Plural
%<attr%>     ! Attributive
%<pred%>     ! Predicative
%<abs%>      ! Absolutive
%<obj%>      ! Objective 
%<gen%>      ! Genitive 
%<ins%>      ! Instrumental 
%<loc%>      ! Locative
%<abl%>      ! Ablative
%<priv%>     ! Privative 
%<fin%>      ! Final(?)
%<dur%>      ! Durative
%<term%>     ! Terminative 
%<dist%>     ! Distive 
%<direc%>    ! Directive 
%<pres%>     ! Present
%<past%>     ! Past
%<p1%>       ! First person
%<p2%>       ! Second person
%<p3%>       ! Third person
%<def%>      ! Definite
%<ind%>      ! Indefinite
%<dem%>      ! Demonstrative
%<pers%>     ! Personal
%<top%>      ! Toponym
%<ant%>      ! Anthroponym 
%<abbr%>     ! Abbreviation

%<pp%>       ! Past participle
%<pprs%>     ! Present participle
%<fp%>       ! Future participle
%<inf%>      ! Verbal noun / infinitive 
%<conv%>     ! Converb 
%<neg%>      ! Negative

%<clb%>      ! Clause level boundary
%<lquot%>    ! Left quote 
%<rquot%>    ! Right quote


%>           ! Morpheme boundary
%{а%}        ! Archivowel: а or е 
%{ӑ%}        ! Archivowel: ӑ or ӗ
%{р%}        ! Archiconsonant: р оr т 
%{н%}        ! Archiconsonant: н or м 
%{ь%}        ! ь or ''

LEXICON Root

NominalStems ;
Pronouns ;
VerbStems ;
ProperNouns ;
AdjStems ;
Numerals ;
Adverbs ; 
Postpos ; 
Particles ; 
Conjunctions ; 
Interjections ; 
Punct ; 

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!                  C O N T I N U A T I O N      L E X I C A               !!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!


LEXICON NOM-DECL-C

%<abs%>: # ;

! 1.  -ø          Optional with any stem
! 2.  >Ăn         After polysyllabic consonant stems and vocalic stems in -U employing
!                their stem alternant in -Ăv to replace U; some monosyllabic consonant
!                stems
! 3.  >n or >Ăn   After polysyllabic consonant stems except U and A
! 4.  C¹C¹Ă>N     After polysyllabic vowel stems in >-C¹Ă
! 5.  C¹C²Ă>n     After polysyllabic vowel stems in >-C¹C²Ă
! 6.  C¹>Ǎn       After borrowed polysyllabic stems in C¹C¹
! 7.  C¹C¹>Ăn     After some monosyllabic stems in C¹
! 8.  >n or >jěn  After native and foreign stems in -i
! 9.  -ă>n        After borrowed vowel stems in -a, -ja, -e, -u and some in -o
! 10. -o>n        After some borrowed vowel stems in -o
! 11. -s>ăn       After borrowed stems in -st'      

%<gen%>:%>%{а%}н # ;        ! 

%<obj%>%<ind%>: # ;          ! 
%<obj%>%<def%>:%>%{а%} # ;          ! 
%<loc%>:%>%{р%}%{а%} # ;        ! 
%<abl%>:%>%{р%}%{а%}н # ;        ! 
%<ins%>:%>п%{а%} # ;        ! 
%<fin%>:%>ш%{ӑ%}н # ;      ! 
%<priv%>:%>с%{ӑ%}р # ;      ! 
%<term%>:%>чен # ;          ! 
%<dist%>:%>серен # ;          ! 
%<direc%>:%>%{а%}лл%{а%} # ;          ! 


LEXICON NOM-DECL-V

%<abs%>: # ;
%<gen%>:%>%{а%}н # ;        ! 

%<obj%>%<ind%>: # ;          ! 
%<obj%>%<def%>:%>%{а%} # ;          ! 
%<loc%>:%>%{р%}%{а%} # ;        ! 
%<abl%>:%>%{р%}%{а%}н # ;        ! 
%<ins%>:%>п%{а%} # ;        ! 
%<fin%>:%>ш%{ӑ%}н # ;      ! 
%<priv%>:%>с%{ӑ%}р # ;      ! 
%<term%>:%>чен # ;          ! 
%<dist%>:%>серен # ;          ! 
%<direc%>:%>лл%{а%} # ;          ! 

LEXICON NOM-C-REG

%<n%>: NOM-DECL-C ;
%<n%>%<pl%>:%>се%{н%} NOM-DECL-C ;

LEXICON NOM-V-REG

%<n%>: NOM-DECL-V ;
%<n%>%<pl%>:%>се%{н%} NOM-DECL-V ;

LEXICON NP-C

%<np%>: NOM-DECL-C ;

LEXICON NP-TOP-C

%<np%>%<top%>: NOM-DECL-C ;

LEXICON NP-ANT-MSC-C

%<np%>%<ant%>%<m%>: NOM-DECL-C ;

LEXICON NP-ANT-FEM-V

%<np%>%<ant%>%<f%>: NOM-DECL-V ;

LEXICON NP-V

%<np%>: NOM-DECL-V ;

LEXICON NP-TOP-V

%<np%>%<top%>: NOM-DECL-V ;

LEXICON NP-ANT-MSC-V

%<np%>%<ant%>%<m%>: NOM-DECL-V ;

LEXICON NUMshort

%<num%>%<attr%>: # ;

LEXICON NUMlong

%<num%>%<pred%>: # ;

LEXICON NUM-ABBR

%<num%>%<abbr%>: # ;


LEXICON ADV 

%<adv%>: # ;

LEXICON POST 

%<post%>: # ;

LEXICON CLB

%<clb%>: # ;

LEXICON ADJ

%<adj%>: # ;

LEXICON CC 

%<cnjcoo%>: # ; 

LEXICON CS

%<cnjsub%>: # ; 

LEXICON PART

%<part%>: # ; 

LEXICON V-PRES-CONJ

%<dur%>%<pres%>%<p1%>%<sg%>:%>т%{ӑ%}р # ;
%<dur%>%<pres%>%<p2%>%<sg%>:%>т%{ӑ%}н # ; 
%<dur%>%<pres%>%<p3%>%<sg%>:%>т%{ь%} # ;
%<dur%>%<pres%>%<p1%>%<pl%>:%>тп%{ӑ%}р # ;
%<dur%>%<pres%>%<p2%>%<pl%>:%>т%{ӑ%}р # ; 
%<dur%>%<pres%>%<p3%>%<pl%>:%>шшӗ # ;

LEXICON C-PRES-CONJ

%<dur%>%<pres%>%<p1%>%<sg%>:%>%{а%}т%{ӑ%}р # ;
%<dur%>%<pres%>%<p2%>%<sg%>:%>%{а%}т%{ӑ%}н # ; 
%<dur%>%<pres%>%<p3%>%<sg%>:%>%{а%}т%{ь%} # ;
%<dur%>%<pres%>%<p1%>%<pl%>:%>%{а%}тп%{ӑ%}р # ;
%<dur%>%<pres%>%<p2%>%<pl%>:%>%{а%}т%{ӑ%}р # ; 
%<dur%>%<pres%>%<p3%>%<pl%>:%>%{а%}шшӗ # ;

LEXICON C-PAST-CONJ

%<past%>%<p1%>%<sg%>:%>т%{ӑ%}м # ;
%<past%>%<p2%>%<sg%>:%>т%{ӑ%}н # ; 
%<past%>%<p3%>%<sg%>:%>ч%{ӑ%} # ;
%<past%>%<p1%>%<pl%>:%>т%{ӑ%}м%{ӑ%}р # ;
%<past%>%<p2%>%<pl%>:%>т%{ӑ%}р # ; 
%<past%>%<p3%>%<pl%>:%>ч%{ӑ%}ҫ # ;


LEXICON V-STEM-CONJ

%<pprs%>:%>к%{а%}н NOM-DECL-C ;
%<pp%>:%>н%{ӑ%} # ;
%<fp%>:%>%{а%}с # ;
%<inf%>:%>м%{а%} # ;
%<conv%>:%>с%{а%} # ;
%<neg%>:%>м%{а%}с V-PRES-CONJ ;
V-PRES-CONJ ; 

!%<der‣caus%>%<v%>:%>тар Vreg ; 

LEXICON C-STEM-CONJ

%<pprs%>:%>к%{а%}н NOM-DECL-C ;
%<pp%>:%>н%{ӑ%} # ;
%<fp%>:%>%{а%}с # ;
%<inf%>:%>м%{а%} # ;
%<conv%>:%>с%{а%} # ;
%<neg%>:%>м%{а%}с C-PRES-CONJ ;
C-PRES-CONJ ; 
C-PAST-CONJ ; 

!%<der‣caus%>%<v%>:%>тар Vreg ; 

LEXICON V-STEM-V-REG

%<v%>: V-STEM-CONJ ;

LEXICON C-STEM-V-REG

%<v%>: C-STEM-CONJ ;

LEXICON Pronouns 

!! Review forms with -(ăn)

епӗ%<prn%>%<pers%>%<p1%>%<sg%>%<abs%>:епӗ # ;
епӗ%<prn%>%<pers%>%<p1%>%<sg%>%<obj%>:ман # ;
епӗ%<prn%>%<pers%>%<p1%>%<sg%>%<obj%>:манӑн # ;
епӗ%<prn%>%<pers%>%<p1%>%<sg%>%<gen%>:мана # ;
епӗ%<prn%>%<pers%>%<p1%>%<sg%>%<loc%>:манра # ;
епӗ%<prn%>%<pers%>%<p1%>%<sg%>%<abl%>:манран # ;
епӗ%<prn%>%<pers%>%<p1%>%<sg%>%<ins%>:манпа # ;

есӗ%<prn%>%<pers%>%<p2%>%<sg%>%<abs%>:есӗ # ;
есӗ%<prn%>%<pers%>%<p2%>%<sg%>%<obj%>:сан # ;
есӗ%<prn%>%<pers%>%<p2%>%<sg%>%<obj%>:санӑн # ;
есӗ%<prn%>%<pers%>%<p2%>%<sg%>%<gen%>:сана # ;
есӗ%<prn%>%<pers%>%<p2%>%<sg%>%<loc%>:санра # ;
есӗ%<prn%>%<pers%>%<p2%>%<sg%>%<abl%>:санран # ;
есӗ%<prn%>%<pers%>%<p2%>%<sg%>%<ins%>:санпа # ;

вӑл%<prn%>%<pers%>%<p3%>%<sg%>%<abs%>:вӑл # ;
вӑл%<prn%>%<pers%>%<p3%>%<sg%>%<obj%>:ун # ;
вӑл%<prn%>%<pers%>%<p3%>%<sg%>%<obj%>:унӑн # ;
вӑл%<prn%>%<pers%>%<p3%>%<sg%>%<gen%>:ӑна # ;
вӑл%<prn%>%<pers%>%<p3%>%<sg%>%<loc%>:унра # ;
вӑл%<prn%>%<pers%>%<p3%>%<sg%>%<abl%>:унран # ;
вӑл%<prn%>%<pers%>%<p3%>%<sg%>%<ins%>:унпа # ;

епир%<prn%>%<pers%>%<p1%>%<pl%>%<abs%>:епир #;
епир%<prn%>%<pers%>%<p1%>%<pl%>%<obj%>:пире #;
епир%<prn%>%<pers%>%<p1%>%<pl%>%<gen%>:пирӗн #;
епир%<prn%>%<pers%>%<p1%>%<pl%>%<loc%>:пирӗнте #;
епир%<prn%>%<pers%>%<p1%>%<pl%>%<abl%>:пирӗнтен #;
епир%<prn%>%<pers%>%<p1%>%<pl%>%<ins%>:пирӗнпе #;

есир%<prn%>%<pers%>%<p2%>%<pl%>%<abs%>:есир #;
есир%<prn%>%<pers%>%<p2%>%<pl%>%<obj%>:сире #;
есир%<prn%>%<pers%>%<p2%>%<pl%>%<gen%>:сирӗн #;
есир%<prn%>%<pers%>%<p2%>%<pl%>%<loc%>:сирӗнте #;
есир%<prn%>%<pers%>%<p2%>%<pl%>%<abl%>:сирӗнтен #;
есир%<prn%>%<pers%>%<p2%>%<pl%>%<ins%>:сирӗнпе #;

вӗсем%<prn%>%<pers%>%<p3%>%<pl%>%<abs%>:вӗсем #;
вӗсем%<prn%>%<pers%>%<p3%>%<pl%>%<obj%>:вӗсен #;
вӗсем%<prn%>%<pers%>%<p3%>%<pl%>%<obj%>:вӗсенӗн #;
вӗсем%<prn%>%<pers%>%<p3%>%<pl%>%<gen%>:вӗсене #;
вӗсем%<prn%>%<pers%>%<p3%>%<pl%>%<loc%>:вӗсенче #;
вӗсем%<prn%>%<pers%>%<p3%>%<pl%>%<abl%>:вӗсенчен #;
вӗсем%<prn%>%<pers%>%<p3%>%<pl%>%<ins%>:вӗсемпе #;

леш%<prn%>%<dem%>%<sg%>%<abs%>:леш  # ; 
леш%<prn%>%<dem%>%<sg%>%<abs%>:лешӗ  # ; 
леш%<prn%>%<dem%>%<sg%>%<obj%>:лешӗн # ; 
леш%<prn%>%<dem%>%<sg%>%<obj%>:лешӗнче # ; 
леш%<prn%>%<dem%>%<sg%>%<obj%>:лешӗнчен # ; 
леш%<prn%>%<dem%>%<sg%>%<obj%>:лешпе # ; 
леш%<prn%>%<dem%>%<pl%>%<abs%>:лешсем  # ; 
леш%<prn%>%<dem%>%<pl%>%<abs%>:лешсенӗ  # ; 
леш%<prn%>%<dem%>%<pl%>%<obj%>:лешсенӗн # ; 
леш%<prn%>%<dem%>%<pl%>%<obj%>:лешсенче # ; 
леш%<prn%>%<dem%>%<pl%>%<obj%>:лешсенчен # ; 
леш%<prn%>%<dem%>%<pl%>%<loc%>:лешсемпе # ; 

ку%<prn%>%<dem%>%<sg%>%<abs%>:ку  # ; 
ку%<prn%>%<dem%>%<sg%>%<obj%>:кун # ; 
ку%<prn%>%<dem%>%<sg%>%<loc%>:кунпа # ; 
ку%<prn%>%<dem%>%<pl%>%<abs%>:кунсем  # ; 
ку%<prn%>%<dem%>%<pl%>%<abs%>:кусенӗ  # ; 
ку%<prn%>%<dem%>%<pl%>%<obj%>:кусенӗн # ; 
ку%<prn%>%<dem%>%<pl%>%<obj%>:кусенче # ; 
ку%<prn%>%<dem%>%<pl%>%<obj%>:кусенчен # ; 
ку%<prn%>%<dem%>%<pl%>%<loc%>:кусемпа # ; 



!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!                  L E X I C O N    E N T R I E S                         !!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

LEXICON Numerals

пӗр NUMshort ;
пӗр:пӗрре NUMlong ;

пилӗк NUMshort ;
пилӗк:пиллӗк NUMlong ;

млн%.:млн%. NUM-ABBR ;

LEXICON AdjStems

шӗнӗ ADJ ;

LEXICON NominalStems

завод NOM-C-REG ; 
католик NOM-C-REG ; 
колхозник NOM-C-REG ; 
кун NOM-C-REG ;
мир NOM-C-REG ; 
поезд NOM-C-REG ; 
сын NOM-C-REG ; 
фабрик NOM-C-REG ; 
чечек NOM-C-REG ; 
ҫурҫӗр NOM-C-REG ; 
кӑнтӑр NOM-C-REG ; 
департамент NOM-C-REG ; 

ҫӗре NOM-V-REG ; 
ача NOM-V-REG ;
кӗнеке NOM-V-REG ;
ту NOM-V-REG ;
тӑва NOM-V-REG ;
тӳпе NOM-V-REG ;
уй NOM-V-REG ;
чикӗ NOM-V-REG ;
фонетика NOM-V-REG ;

LEXICON ProperNouns

Турӑ NP-V ; 
Шупашкар NP-TOP-C ; 
Мадрид NP-TOP-C ; 
Любляна NP-TOP-V ; 
Мускав NP-TOP-C ; 
Петӗр NP-ANT-MSC-C ; 
Иван NP-ANT-MSC-C ; 
Владимир NP-ANT-MSC-C ; 
Марина NP-ANT-FEM-V ; 
Ирина NP-ANT-FEM-V ; 
Яна NP-ANT-FEM-V ; 

LEXICON Adverbs

пуҫламӑшенче ADV ;
нумай ADV ;
киле ADV ;
каҫпа ADV ;
кунӗпа ADV ;
паян ADV ;
ернипе ADV ;
ҫӗрӗпе ADV ;
паян% кунӗпа ADV ;

LEXICON Postpos 

урлӑ POST ; 
пӗрле POST ; 
витӗр POST ; 
пек POST ; 
таран POST ; 
тӑрӑх POST ; 
пула POST ; 
пирки POST ; 
валли POST ; 
кура POST ; 
хирӗҫ POST ; 
пуҫне POST ; 
тавра POST ; 

LEXICON Conjunctions 

тата CC ; 
те CC ; 

LEXICON Particles 

акӑ PART ;

LEXICON VerbStems 

пул C-STEM-V-REG ;
кил C-STEM-V-REG ;
ил C-STEM-V-REG ;
сыp C-STEM-V-REG ;
тух C-STEM-V-REG ;
туп C-STEM-V-REG ;
тат C-STEM-V-REG ;
юх C-STEM-V-REG ;

тӑва V-STEM-V-REG ;
кая V-STEM-V-REG ;
кай C-STEM-V-REG ;
сывла V-STEM-V-REG ;
вула V-STEM-V-REG ;
ӗҫле V-STEM-V-REG ;
те V-STEM-V-REG ;

пырса% кала V-STEM-V-REG ;
тухса% кай C-STEM-V-REG ;

LEXICON Interjections 

пысак% тавтапуҫ%<ij%>:пысак% тавтапуҫ # ;

LEXICON Punct

. CLB ; 
? CLB ; 
%! CLB ; 
»%<rquot%>:» # ;
«%<lquot%>:« # ; 
