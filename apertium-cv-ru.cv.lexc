Multichar_Symbols

! Multichar symbols are enclosed in < > according to Apertium style, 
! archigraphemes are enclosed in { } according to Omorfi style

%<n%>        ! Noun
%<np%>       ! Proper noun
%<v%>        ! Verb
%<adv%>      ! Adverb
%<num%>      ! Numeral
%<adj%>      ! Adjective
%<prn%>      ! Pronoun
%<post%>     ! Postposition
%<cnjcoo%>   ! Coordinating conjunction
%<cnjsub%>   ! Subordinating conjunction
%<part%>     ! Particle

%<der‣caus%> ! Derivation (V→V): Causative

%<sg%>       ! Singular
%<pl%>       ! Plural
%<attr%>     ! Attributive
%<pred%>     ! Predicative
%<abs%>      ! Absolutive
%<obj%>      ! Objective 
%<gen%>      ! Genitive 
%<ins%>      ! Instrumental 
%<loc%>      ! Locative
%<abl%>      ! Ablative
%<pp%>       ! Past participle
%<pprs%>     ! Present participle
%<dur%>      ! Durative
%<pres%>     ! Present
%<p1%>       ! First person
%<p2%>       ! Second person
%<p3%>       ! Third person
%<def%>      ! Definite
%<ind%>      ! Indefinite
%<pers%>     ! Personal
%<top%>      ! Toponym

%<clb%>      ! Clause level boundary
%<lquot%>    ! Left quote 
%<rquot%>    ! Right quote


%>           ! Morpheme boundary
%{а%}        ! Archivowel: а or е 
%{ӑ%}        ! Archivowel: ӑ or ӗ
%{р%}        ! Archiconsonant: р оr т 
%{н%}        ! Archiconsonant: н or м 
%{ь%}        ! ь or ''

LEXICON Root

NominalStems ;
Pronouns ;
VerbStems ;
ProperNouns ;
AdjStems ;
Numerals ;
Adverbs ; 
Postpos ; 
Particles ; 
Conjunctions ; 
Punct ; 

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! Paradigms 
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

LEXICON Decl1 

%<abs%>: # ;
%<gen%>:%>%{а%}н # ;        ! 
%<obj%>%<ind%>: # ;          ! 
%<obj%>%<def%>:%>%{а%} # ;          ! 
%<loc%>:%>%{р%}%{а%} # ;        ! 
%<abl%>:%>%{р%}%{а%}н # ;        ! 
%<ins%>:%>п%{а%} # ;        ! 
%<fin%>:%>ш%{ӑ%}н # ;      ! 
%<priv%>:%>с%{ӑ%}р # ;      ! 
%<term%>:%>чен # ;          ! 
%<dist%>:%>серен # ;          ! 

LEXICON Nreg

%<n%>: Decl1 ;
%<n%>%<pl%>:%>се%{н%} Decl1 ;

LEXICON Nprop

%<np%>: Decl1 ;

LEXICON Ntop

%<np%>%<top%>: Decl1 ;

LEXICON NUMshort

%<num%>%<attr%>: # ;

LEXICON NUMlong

%<num%>%<pred%>: # ;

LEXICON ADV 

%<adv%>: # ;

LEXICON POST 

%<post%>: # ;

LEXICON CLB

%<clb%>: # ;

LEXICON ADJ

%<adj%>: # ;

LEXICON CC 

%<cnjcoo%>: # ; 

LEXICON CS

%<cnjsub%>: # ; 

LEXICON PART

%<part%>: # ; 

LEXICON Vconj

%<pprs%>:%>к%{а%}н Decl1 ;
%<pp%>:%>н%{ӑ%} # ;
%<dur%>%<pres%>%<p1%>%<sg%>:%>т%{ӑ%}р # ;
%<dur%>%<pres%>%<p2%>%<sg%>:%>т%{ӑ%}н # ; 
%<dur%>%<pres%>%<p3%>%<sg%>:%>т%{ь%} # ;
%<dur%>%<pres%>%<p1%>%<pl%>:%>тп%{ӑ%}р # ;
%<dur%>%<pres%>%<p2%>%<pl%>:%>т%{ӑ%}р # ; 
%<dur%>%<pres%>%<p3%>%<pl%>:%>шшӗ # ;
!%<der‣caus%>%<v%>:%>тар Vreg ; 

LEXICON Vreg

%<v%>: Vconj ;

LEXICON Pronouns 

!! Review forms with -(ăn)

епĕ%<prn%>%<pers%>%<p1%>%<sg%>%<abs%>:епĕ # ;
епĕ%<prn%>%<pers%>%<p1%>%<sg%>%<obj%>:ман # ;
епĕ%<prn%>%<pers%>%<p1%>%<sg%>%<obj%>:манӑн # ;
епĕ%<prn%>%<pers%>%<p1%>%<sg%>%<gen%>:мана # ;
епĕ%<prn%>%<pers%>%<p1%>%<sg%>%<loc%>:манра # ;
епĕ%<prn%>%<pers%>%<p1%>%<sg%>%<abl%>:манран # ;
епĕ%<prn%>%<pers%>%<p1%>%<sg%>%<ins%>:манпа # ;

есĕ%<prn%>%<pers%>%<p2%>%<sg%>%<abs%>:есĕ # ;
есĕ%<prn%>%<pers%>%<p2%>%<sg%>%<obj%>:сан # ;
есĕ%<prn%>%<pers%>%<p2%>%<sg%>%<obj%>:санӑн # ;
есĕ%<prn%>%<pers%>%<p2%>%<sg%>%<gen%>:сана # ;
есĕ%<prn%>%<pers%>%<p2%>%<sg%>%<loc%>:санра # ;
есĕ%<prn%>%<pers%>%<p2%>%<sg%>%<abl%>:санран # ;
есĕ%<prn%>%<pers%>%<p2%>%<sg%>%<ins%>:санпа # ;

вӑл%<prn%>%<pers%>%<p3%>%<sg%>%<abs%>:вӑл # ;
вӑл%<prn%>%<pers%>%<p3%>%<sg%>%<obj%>:ун # ;
вӑл%<prn%>%<pers%>%<p3%>%<sg%>%<obj%>:унӑн # ;
вӑл%<prn%>%<pers%>%<p3%>%<sg%>%<gen%>:ӑна # ;
вӑл%<prn%>%<pers%>%<p3%>%<sg%>%<loc%>:унра # ;
вӑл%<prn%>%<pers%>%<p3%>%<sg%>%<abl%>:унран # ;
вӑл%<prn%>%<pers%>%<p3%>%<sg%>%<ins%>:унпа # ;

епир%<prn%>%<pers%>%<p1%>%<pl%>%<abs%>:епир #;
епир%<prn%>%<pers%>%<p1%>%<pl%>%<obj%>:пире #;
епир%<prn%>%<pers%>%<p1%>%<pl%>%<gen%>:пирĕн #;
епир%<prn%>%<pers%>%<p1%>%<pl%>%<loc%>:пирĕнте #;
епир%<prn%>%<pers%>%<p1%>%<pl%>%<abl%>:пирĕнтен #;
епир%<prn%>%<pers%>%<p1%>%<pl%>%<ins%>:пирĕнпе #;

есир%<prn%>%<pers%>%<p2%>%<pl%>%<abs%>:есир #;
есир%<prn%>%<pers%>%<p2%>%<pl%>%<obj%>:сире #;
есир%<prn%>%<pers%>%<p2%>%<pl%>%<gen%>:сирĕн #;
есир%<prn%>%<pers%>%<p2%>%<pl%>%<loc%>:сирĕнте #;
есир%<prn%>%<pers%>%<p2%>%<pl%>%<abl%>:сирĕнтен #;
есир%<prn%>%<pers%>%<p2%>%<pl%>%<ins%>:сирĕнпе #;

вĕсем%<prn%>%<pers%>%<p3%>%<pl%>%<abs%>:вĕсем #;
вĕсем%<prn%>%<pers%>%<p3%>%<pl%>%<obj%>:вĕсен #;
вĕсем%<prn%>%<pers%>%<p3%>%<pl%>%<obj%>:вĕсенĕн #;
вĕсем%<prn%>%<pers%>%<p3%>%<pl%>%<gen%>:вĕсене #;
вĕсем%<prn%>%<pers%>%<p3%>%<pl%>%<loc%>:вĕсенче #;
вĕсем%<prn%>%<pers%>%<p3%>%<pl%>%<abl%>:вĕсенчен #;
вĕсем%<prn%>%<pers%>%<p3%>%<pl%>%<ins%>:вĕсемпе #;

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! Entries 
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

LEXICON Numerals

пĕр NUMshort ;
пĕр:пĕрре NUMlong ;

пилĕк NUMshort ;
пилĕк:пиллĕк NUMlong ;

LEXICON AdjStems

шĕнĕ ADJ ;

LEXICON NominalStems

тӳпе Nreg ;
çĕре Nreg ; 
завод Nreg ; 
фабрик Nreg ; 
кун Nreg ;
ту Nreg ;
ача Nreg ;
чечек Nreg ; 
уй Nreg ;
кĕнеке Nreg ;

LEXICON ProperNouns

Турӑ Nprop ; 
Шупашкар Ntop ; 

LEXICON Adverbs

пу% çламӑшенче ADV ;
нумай ADV ;
киле ADV ;

LEXICON Postpos 

урлӑ POST ; 
пĕрле POST ; 
витĕр POST ; 
пек POST ; 
таран POST ; 
тӑрӑх POST ; 
пула POST ; 
пирки POST ; 
валли POST ; 
кура POST ; 
хирĕç POST ; 
пуçне POST ; 

LEXICON Conjunctions 

тата CC ; 

LEXICON Particles 

акӑ PART ;

LEXICON VerbStems 

пул Vreg ;
кил Vreg ;
тӑва Vreg ;
ил Vreg ;

LEXICON Punct

. CLB ; 
? CLB ; 
%! CLB ; 
»%<rquot%>:» # ;
«%<lquot%>:« # ; 
